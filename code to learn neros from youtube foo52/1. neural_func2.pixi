/// проход сигнала с одного слоя нейронов на другой
//  $Ni - первый слой нейронов, $W - матрица весов, $No - второй слой нейронов
//  массив нейронов [ количество, 1  ]
//  массив весов [количество входных нейронов, количество выходных нейронов]
// если в конечном слое нейронов есть bias нейрон, то
//  массив весов [количество входных нейронов, количество выходных нейронов-1]
//=================================
//  in1  in2  in3
//--------------------
//  W14  W24  W34 |  out4
//  W15  W25  W35 |  out5
//================================
fn forWards($Ni,$W,$No)
{
	$wt = get_xsize($W)
	$ht = get_ysize($W)

	$y=0          //выход
	while $y<$ht  // проходим по всем выходным нейронам
	{
		$No[$y,0] = 0
		$x=0         //вход
		while $x<$wt // проходим по всем входным нейронам
		{
			$No[$y,0] = $No[$y,0]+$Ni[$x,0]*$W[$x,$y]
			$x=$x+1
		}
		$No[$y,0] = 1/( 1 + exp( -1 * $No[$y,0] ))
		$y=$y+1
	}
}



// записываем ошибку в выходном слое
fn fixOutError($IDL,$N)	
{
	$wt = get_xsize($IDL)
	$x = 0
	while $x < $wt
	{
		$N[$x,1] = ($IDL[$x,0] - $N[$x,0]) * $N[$x,0] * (1 - $N[$x,0])
		//if ( ($N[$x,1]>-1*$b) && ($N[$x,1]<$b) )			{ $N[$x,1]=0 }
		$x=$x+1
	}
}

//
fn findError($Ni, $W, $No)
{ 
	$wt = get_xsize($W)
	$ht = get_ysize($W)
	$x=0
	while $x < $wt  // по всем входным нейронам
	{
		$Ni[$x,1] = 0
		$y = 0
		while $y < $ht  // по всем выходным нейронам
		{
			
			$Ni[$x,1] = $Ni[$x,1] + $W[$x,$y] * $No[$y,1]
			$y = $y + 1
		}
		$Ni[$x,1] = $Ni[$x,1] * $Ni[$x,0] * (1 - $Ni[$x,0])
		$x= $x + 1
	}
}


fn backWards($Ni, $W, $No, $k)
{
	$wt = get_xsize($W)
	$ht = get_ysize($W)
	$y=0
	while $y < $ht       // по всем выходным нейронам
	{
		$x = 0
		while $x < $wt   // по всем входным нейронам
		{
			$W[$x,$y] = $W[$x,$y] + $k * $No[$y,1] * $Ni[$x,0]//* $No[$y,0] * (1-$No[$y,0]) 
			$x = $x + 1
		}
		$y= $y + 1
	}
}


// заполнение массива весов случайными чмслами в диапозоне -0,5 .. 0,5
fn fillW($W)
{
	$wt = get_xsize($W)
	$ht = get_ysize($W)

	$x=0
	while $x<$wt
	{
		$y=0
		while $y<$ht
		{
			$W[$x,$y] = ( mod(rand(),1000)/5000) - 0.1
			//$W[$x,$y] =  mod(rand(),10)
			$y=$y+1
		}
		$x=$x+1
	}
}





